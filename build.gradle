import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.4.3'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
    spigotmc()
    jitpack()
}

dependencies {
    compileOnly spigot('1.19.3')
    implementation 'com.github.simplix-softworks:simplixstorage:3.2.5'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

group = 'everyos.plugin.fns'
version = '0.1.0'
description = 'Custom plugin for Fate Stay Night server'
java.sourceCompatibility = JavaVersion.VERSION_17

tasks.named('test') {
    useJUnitPlatform()
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "fsn"
}

tasks.prepareSpigotPlugins.dependsOn shadowJar
tasks.shadowJar.dependsOn tasks.relocateShadowJar

shadowJar {
    classifier = null
}

spigot {
    authors = ['EveryOS']
    apiVersion = '1.19'
    commands {
        itemstats { 
            description = 'Item stats manipulation command'
            usage = '/<command> <subcommand> <statname> [amount]'
        }
        customitem {
            description = 'Custom item manipulation command'
            usage = '/<command> <itemname> <create <rarity>/give <player>/stats <subcommand> [amount]/attributes <subcommand> <statname>/delete>'
        }
        
    }
    excludeLibraries = [
        "*"
    ]
}